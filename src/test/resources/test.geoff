/*
 * Node label and property combinations
 *
 * @order 24
 * @size 0
 * @node a
 * @node b foo=bar
 * @node c foo=bar baz=qux
 * @node d :Foo
 * @node e :Foo foo=bar
 * @node f :Foo foo=bar baz=qux
 * @node g :Foo :Bar
 * @node h :Foo :Bar foo=bar
 * @node i :Foo :Bar foo=bar baz=qux
 * @node j
 * @node k
 * @node l truth=true falsehood=false
 * @node m nothing
 * @node n τ=6.283185307179586
 *
 */

()
({"foo":"bar"})
({"foo":"bar","baz":"qux"})
(:Foo)
(:Foo {"foo":"bar"})
(:Foo {"foo":"bar","baz":"qux"})
(:Foo:Bar)
(:Foo:Bar {"foo":"bar"})
(:Foo:Bar {"foo":"bar","baz":"qux"})

(a)
(b {"foo":"bar"})
(c {"foo":"bar","baz":"qux"})
(d:Foo)
(e:Foo {"foo":"bar"})
(f:Foo {"foo":"bar","baz":"qux"})
(g:Foo:Bar)
(h:Foo:Bar {"foo":"bar"})
(i:Foo:Bar {"foo":"bar","baz":"qux"})

(j {"text": "Lorem ipsum dolor sit amet...", "awkward": "string containing \"substring\"", "rainbow": ["red","orange","yellow","green","blue","indigo","violet"]})
(k {"integer": 42, "fibonacci": [1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144]})
(l {"truth": true, "falsehood": false, "answers": [true, false, true, true, false]})
(m {"nothing": null})
(n {"τ": 6.283185307179586, "L": 6.0221413e+23, "reals": [1.2, 3.4, 5.6, 7.8, 9.0], "mixed1": [1, 2.3, 4], "mixed2": [1.2, 3, 4.5]})

("x y \"z\"":Foo {"foo":"bar"})

~~~~
/*
 * Triangle of bidirectional relationships
 *
 * @order 3
 * @size 6
 * @node alice :Person!name name=Alice
 * @node bob :Person!name name=Bob
 * @node carol :Person!name name=Carol
 * @rel alice KNOWS bob
 * @rel alice KNOWS carol
 * @rel bob KNOWS alice
 * @rel bob KNOWS carol
 * @rel carol KNOWS alice
 * @rel carol KNOWS bob
 *
 */

:Person:name:=>(alice {"name":"Alice"})
:Person:name:=>(bob {"name":"Bob"})
:Person:name:=>(carol {"name":"Carol"})

(alice)<-[:KNOWS]->(bob)<-[:KNOWS]->(carol)<-[:KNOWS]->(alice)

~~~~
/*
 * Triangle of unique bidirectional relationships
 *
 * @order 3
 * @size 6
 * @node alice :Person!name name=Alice
 * @node bob :Person!name name=Bob
 * @node carol :Person!name name=Carol
 * @rel alice KNOWS! bob
 * @rel alice KNOWS! carol
 * @rel bob KNOWS! alice
 * @rel bob KNOWS! carol
 * @rel carol KNOWS! alice
 * @rel carol KNOWS! bob
 *
 */

(alice:Person!name {"name":"Alice"})
(bob:Person!name {"name":"Bob"})
(carol:Person!name {"name":"Carol"})

(alice)<-[:KNOWS!]->(bob)<-[:KNOWS!]->(carol)<-[:KNOWS!]->(alice)

~~~~
/*
 * Random bits
 *
 * @order 4
 * @size 5
 * @node a
 * @node b :Person name=Bob
 * @node c :Person name=Carol
 * @node d name=Dave age=104
 * @rel a KNOWS b
 * @rel b LIKES a
 * @rel b HATES c
 * @rel c WORKS_WITH a since=1999
 * @rel c KNOWS d foo=bar answer=42
 */

(b:Person {"name": "Bob"})
(a)-[:KNOWS]->(b)
(c:Person) {"name": "Carol", "favourite colours": ["red", "purple"]}
(a)<-[:LIKES]-(b)-[:HATES]->(c)
(c)-[:WORKS_WITH {"since":1999}]->(a)

(c)-[:KNOWS]->(d {"name":"Dave", "age": 104}) {"foo":"bar","answer":42}